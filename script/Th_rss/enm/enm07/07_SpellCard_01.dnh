//#東方弾幕風
#ScriptVersion[3]
#Title["桃源宮　stage7　一剣「刀剣を給す打ち出の野槌」"]
#Text[""]
#System["script/Th_rss/system/System_Main.dnh"]
#Image[""]
#Background[""]
	
	//　ライブラリ管理ファイル
	#include "./../lib_Control.dnh"
	#include "./lib_enm07_setting.dnh"
	#include "./lib_enm07_effect.dnh"
	
	let enm;
	let spl;
	
	//　カウンタ
	let count		= 0;
	
	//　制限時間
	let SET_TIMER	= 66;
	
	@Initialize{
		//　ボス生成
		enm	= ObjEnemy_Create(OBJ_ENEMY_BOSS);
		ObjEnemy_SetDamageRate( enm, 0, 0 );
		ObjEnemy_Regist( enm );
		
		//　初期化
		EnemyInitialize( enm );
		
		//　体力0まで待機
		EndEnemy( enm );
		
		//　敵ダメージレート 第2引数:ボムバリア
		SetDamageRate( enm, 100, true );
		ShotScore( enm );
		
		//　スペルカード宣言
		CallSpellCard( enm, 0 );
		
		//　削除上限
		SetShotAutoDeleteClip( 96, 64, 96, 96 );
	}
	
	@Loading{
		//　弾データ読み込み　ALPHAとADD
		LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data.dnh" );
		LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data_ADD.dnh" );
	}
	
	@MainLoop{
		//　敵の当たり判定
		SetIntersection( enm, true, true );
		
		//　弾幕
		if( count == 180 ){
			ShotMain( enm );
		}
		
		//　カウンタ
		count++;
		
		yield;
	}
	
	@Event{
		alternative( GetEventType() )
		case( EV_REQUEST_LIFE ){
			SetScriptResult( 1400 );
		}
		case( EV_REQUEST_TIMER ){
			//　制限時間
			SetScriptResult( SET_TIMER );
		}
		case( EV_REQUEST_SPELL_SCORE ){
			//　スペルカードボーナス
			SetScriptResult( SET_SCORE );
		}
		case( EV_GAIN_SPELL ){
			//　スペル取得
			SpellCounterGet( GetDifficult, 7, 3 );
		}
	}
	
	@Finalize{ }
	
	//----------------------------------------------------------------------------------------------------
	//----------------------------------------------------------------------------------------------------
	
	//　初期化
	task EnemyInitialize(ID){
		//　アニメーション用共通データ作成
		CreateCommonDataArea( "anime" );
		SetAreaCommonData( "anime", "spa", false );
		
		//　60角形　距離64　優先71　遅延120.f　色[紫]　スペル用　[外枠出現処理ON,本体出現処理ON]
		//　"Default" or "Normal" or "SpellCard"
		CreateLifeBar( ID, 60, 64, 71, 0, [255,255,255], "SpellCard" , [false,false] );
		//　type　delay
		//　0:戦闘開始　1:戦闘継続
		RedMagicCircle( 1, 0 );
		Aura( ID, 8, aura_rgb[0], aura_rgb[1] );
		//　親ID　表示遅延　bool 移動効果　char 移動効果種別　bool フェードイン
		//　"BeginSpellCard" or "EndSpellCard"
		SetDrawTimer( enm, 0, true, "BeginSpellCard", false );
		//　画像パスだけ変えればいいんじゃないかな
		EnemyDisplay_Enm07b( enm, true, enemy_tex_path );
		//　敵位置初期化
		ObjMove_SetDestAtWeight( enm, CenX, CenY-96, 8, 8 );
	}
	
	//　スペカ宣言
	task CallSpellCard(ID,long){
		Wait(long);
		spl	= GetEnemyBossSceneObjectID();
		ObjEnemyBossScene_StartSpell( spl );
		
		let spell_text = "";
		alternative( GetDifficult )
		case( "Extra" ){
			spell_text = "一剣「刀剣を給す打ち出の野槌」";
		}
		others{
			spell_text = "一剣「刀剣を給す打ち出の野槌」";
		}
		
		SpellCounter( GetDifficult, 7, 3 );
		let sct = GetSpellCounter( GetDifficult, 7, 3 );
		
		CutinSP2(ID, spell_text, 0, 0, true );
		SpellCardCutin(ID, "TIREIDEN", 220, 192, [1,1], enemy_cutin_path[1], 512, 512, true );
		SpellCardBonusDisplay(spl,ID );
		SpellCardHistoryDisplay(ID, sct[1], sct[0] );
		SetBackGroundEffect_07b(ID);
		SpellCardMagicTimer(ID);
	}
	
	//　遅延
	function Wait( long ){
		loop( long ){
			yield;
			if( Obj_IsDeleted( enm ) ){ return true; }
		}
		return false;
	}
	
	//　ショットメイン
	task ShotMain( ID ){
		Concentration03_Main( ID, power_rgb, 60 );
		Wait( 100 );
		
		let r = 1;
		while( !Obj_IsDeleted(ID) ){
			let y	= MinY;
			let ag	= 90;
			
			if( GetPlayerY() < CenY ){
				y	= MaxY;
				ag	= 270;
				r = -r;
			}
			
			let shote = [
				BGW_GREEN_17, BGW_AQUA_17,
			];
			let sx = MinX;
			let vx = 0;
			let vp = 20;
			let lp = 0;
			if( r == -1 ){ sx = MaxX; }
			while( vx < ( MaxX - MinX ) * 1.2 ){
				let v = 48 * r;
				let spos	= [ sx + vx * r, y ];
				Shot01( ID, spos, ag, v, shote[lp%2], lp );
				
				vx += vp;
				vp *= 1.26;
				lp++;
				if( Wait( 8 ) ){ return; }
			}
			
			if( Wait( 210 ) ){ return; }
			
			EnemyMove( ID, 2, 8 );
			r = -r;
			yield;
		}
	}
	
	task Shot01( ID, pos, angle, m, shote, lp ){
		let sht		= CreateLaserC( pos[0], pos[1], angle, 0, 0, 0, 10, [ 32, 255, 32 ], 0.02, 1, ADD_BGW_WHITE_30 );
		let frame	= lp * 3;
		let d		= 2;
		ObjSound_Play( CreateShotSE[ 3 ] );
		ObjShot_SetAutoDelete( sht, false );
		ObjShot_SetSpellResist( sht, true );
		let sbase = 2;
		while( !Obj_IsDeleted(sht) && !Obj_IsDeleted(ID) ){
			let gpos = [ ObjMove_GetX(sht), ObjMove_GetY(sht) ];
			
			ObjMove_SetSpeed( sht, sbase + 1 * sin( -frame ) );
			ObjMove_SetAngle( sht, angle + m * sin( frame * 2 ) );
			
			if( frame%10 == 0 ){
				let ag = ObjMove_GetAngle( sht ) + round( rand( -1, 1 ) ) * 2;
				let obj = CreateShot01( gpos, 0, ag, 0.01, 1, shote, 16 );
				ObjSound_Play( CreateShotSE[12] );
			}
			sbase += 0.02;
			frame++;
			if( gpos[1] > MaxY + 255 ){ break; }
			yield;
		}
		Obj_Delete(sht);
	}
	